      SUBROUTINE USER( INFILE)
C  MATRIX GENERATOR FOR PLANT AND TRANSPORTATION PROBLEMS
C   WRITTEN BY L. E.  SCHRAGE AND A. N. KEPNER
C  INPUT:
C    INFILE = CURRENT STANDARD INPUT UNIT NUMBER
C
      CHARACTER*1 KBLANK, KALPHA, KVNAME
      DIMENSION KVNAME( 8), IRO( 4), VAL( 4)
      DIMENSION CAP( 50), FXD( 50), D( 300)
      LOGICAL TRUBLE
      DIMENSION KALPHA( 36)
C  OUTPUT UNIT NUMBER
      DATA LOUT/6/
C  THE ALPHABET
      DATA KALPHA/'0','1','2','3','4','5','6','7','8',
     * '9','A','B','C','D','E','F','G','H','I','J','K','L',
     * 'M','N','O','P','Q','R','S','T','U','V','W','X','Y','Z'/
      DATA KBLANK/' '/
C
C  MAX NUMBER OF PLANTS
      NDIM1 = ( 50)
C  MAX NUMBER OF CUSTOMERS
      NDIM2 = ( 300)
      TRUBLE = .FALSE.
C
   50 WRITE( LOUT, 51)
   51 FORMAT(/,' INPUT FOR PLANT AND TRANSPORTATION PROBLEM.',/,
     1 /,' NO. OF PLANTS AND CUSTOMERS= ',$)
      READ( INFILE, *) NP, NC
      IF ( NP .GT. NDIM1) WRITE( LOUT, 2001) NDIM1
      IF ( NC .GT. NDIM2) WRITE( LOUT, 2002) NDIM2
      IF ( NP .LT. 1) WRITE( LOUT, 2005) NP
      IF ( NC .LT. 1) WRITE( LOUT, 2006) NC
      IF (( NP .GT. NDIM1) .OR. ( NC .GT. NDIM2)) GO TO 50
      IF (( NP .LT. 1) .OR. ( NC .LT. 1)) GO TO 50
      WRITE( LOUT, 52)
   52 FORMAT(' INPUT EACH PLANT CAPACITY AND FIXED COST PAIR,',
     1 /, ' ONE PAIR PER LINE')
      DO 100 I = 1, NP
      WRITE( LOUT, 53) I
   53 FORMAT( 1X, I3, ': ',$)
      READ( INFILE,*) CAP( I), FXD( I)
  100 CONTINUE
      WRITE( LOUT, 104)
  104 FORMAT(/,' INPUT CUSTOMER DEMANDS, ONE PER LINE')
      DO 200 I = 1, NC
      WRITE( LOUT, 195) I
  195 FORMAT( 1X, I4,': ',$)
      READ( INFILE, *) D( I)
  200 CONTINUE
C
C  INITIALIZE THE STORAGE
      CALL INIT
C
C  GENERATE THE ROWS
C
C  THE OBJECTIVE
C  MIN SUM(I,J: C( I,J)* X(I,J) ) + SUM( I: FXD( I)* Y( I))
      CALL DEFROW(1, 0., IDROW, TRUBLE)
C
C  THE CAPACITY ROWS
C  FOR EACH I: SUM( J: X( I,J) ) - K( I)* Y( I) <= 0
      DO 300 I = 1, NP
      CALL DEFROW( 1, 0., IDROW, TRUBLE)
  300 CONTINUE
C
C  THE DEMAND ROWS
C  FOR EACH J: SUM(I: X( I,J)) = D( J)
      DO 400 I = 1, NC
      CALL DEFROW( 0, D( I), IDROW, TRUBLE)
  400 CONTINUE
C
C  GENERATE THE Y VARIABLES
      KVNAME( 1) = KALPHA( 35)
      KVNAME( 5) = KBLANK
      KVNAME( 6) = KBLANK
      KVNAME( 7) = KBLANK
      KVNAME( 8) = KBLANK
      NONZ = 2
      IRO( 1) = 1
      DO 500 I = 1, NP
      VAL( 1) = FXD( I)
      VAL( 2) = - CAP( I)
      IRO( 2) = I + 1
C  SET I1, I2, I3 = 3 DIGITS OF PLANT NO.
      I1 = I/ 100
      ITEMP =  I  - I1*100
      I2 = ITEMP/10
      I3 = ITEMP- I2 * 10
      KVNAME( 2) = KALPHA( I1 + 1)
      KVNAME( 3) = KALPHA( I2 + 1)
      KVNAME( 4) = KALPHA( I3 + 1)
      CALL APPCOL( KVNAME, NONZ, VAL, IRO, TRUBLE)
      ITEMP = I
C
C  MAKE EACH Y 0/1
C
      CALL SETSUB( ITEMP, 1.)
      CALL MAKINT( I)
  500 CONTINUE
C
C  GENERATE THE X VARIABLES
      WRITE( LOUT, 506)
  506 FORMAT(/,' INPUT THE SHIPMENT COSTS/UNIT',/,
     2 ' FOR THE ALLOWED PLANT- CUSTOMER PAIRS IN THE FORM:',/,
     3 ' I,J, C( I,J), E.G. 2, 4, 3.5. ONE TRIPLET/ LINE.'
     4 ,' TERMINATE WITH 0, 0, 0.',/)
      KVNAME( 1) = KALPHA( 34)
      NONZ = 3
      IRO( 1) = 1
C  BEGIN LOOP OVER SHIPMENT COMBINATIONS
  600 READ( INFILE, *) I, J, CIJ
      IF ( I .LE. 0) GO TO 700
      IF ( I .GT. NP) WRITE( LOUT, 2003) I
      IF ( J .GT. NC) WRITE( LOUT, 2004) J
      IF ( J .LT. 1) WRITE( LOUT, 2008) J
      IF (( I .GT. NP) .OR. ( J .GT. NC)) GO TO 600
      IF (( I .LT. 1) .OR. ( J .LT. 1)) GO TO 600
C
C  SET I1, I2, I3 = 3 DIGITS OF PLANT N.
      I1 = I / 100
      ITEMP = I - I1* 10
      I2 = ITEMP / 10
      I3 = ITEMP - I2* 10
      KVNAME(2) = KALPHA( I1 + 1)
      KVNAME( 3) = KALPHA( I2 + 1)
      KVNAME( 4) = KALPHA( I3 + 1)
C  SET I1, I2, I3, I4 = 4 DIGITS OF CUSTOMER NO.
      I1 = J/ 1000
      ITEMP = J - I1* 1000
      I2 = ITEMP/ 100
      ITEMP2 = ITEMP - I2 * 100
      I3 = ITEMP2/ 10
      I4 = ITEMP2 - I3 * 10
      KVNAME( 5) = KALPHA( I1+ 1)
      KVNAME( 6) = KALPHA( I2 + 1)
      KVNAME( 7) = KALPHA( I3 + 1)
      KVNAME( 8) = KALPHA( I4 + 1)
      VAL( 1) = CIJ
      VAL( 2) = 1.
      IRO( 2) = I + 1
      VAL( 3) = 1.
      IRO( 3 ) = 1 + NP + J
      CALL APPCOL( KVNAME, NONZ, VAL, IRO, TRUBLE)
      GO TO 600
C
C  PRINT THE FORMULATION
C
  700 CALL LOOK( 1, IDROW)
C
C  RETURN CONTROL TO THE LINDO COMMAND PROCESSOR
C
      RETURN
C
 2001 FORMAT(/,' NO. PLANTS EXCEEDS MAX ALLOWED:', I6)
 2002 FORMAT(/,' NO. CUSTOMERS EXCEEDS MAX ALLOWED ')
 2003 FORMAT(/,' PLANT NO.', I6,'  IS TOO  LARGE')
 2004 FORMAT(/,' CUSTOMER NO. ', I6, ' IS TOO LARGE.')
 2005 FORMAT(/,' NO. OF PLANTS, ', I6,' IS LESS THAN 1.')
 2006 FORMAT(/,' NO. OF CUSTOMERS IS LESS THAN 1.')
 2007 FORMAT(/,' PLANT NO. ',I6,' IS LESS THAN 1.')
 2008 FORMAT(/,' CUSTOMER NO. ', I6,' IS LESS THAN 1.')
      END
